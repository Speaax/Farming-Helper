plugins {
	id 'java'
}

repositories {
	mavenLocal()
	maven {
		url = 'https://repo.runelite.net'
		content {
			includeGroupByRegex("net\\.runelite.*")
		}
	}
	mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	
	compileOnly 'org.slf4j:slf4j-api:1.7.36'

	testImplementation 'junit:junit:4.12'
	testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion

	// Exclude only the missing LWJGL natives (natives-linux-arm64) from resolution; keep all other natives
	components {
		all { ComponentMetadataDetails details ->
			if (details.id.group == 'net.runelite' &&
					(details.id.name == 'client' || details.id.name == 'jshell')) {
				details.allVariants {
					withDependencies { deps ->
						deps.removeAll { dep ->
							if (dep.group != 'org.lwjgl' ||
									(dep.name != 'lwjgl' && dep.name != 'lwjgl-opengl')) return false
							if (dep instanceof org.gradle.api.artifacts.DirectDependencyMetadata) {
								def selectors = dep.artifactSelectors
								return selectors != null &&
										selectors.any { it.classifier == 'natives-linux-arm64' }
							}
							return false
						}
					}
				}
			}
		}
	}
}

group = 'com.easyfarming'
version = '1.0-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.compilerArgs += ['-Xlint:deprecation']
}

tasks.register('shadowJar', Jar) {
	dependsOn configurations.testRuntimeClasspath
	manifest {
		attributes('Main-Class': 'com.easyfarming.EasyFarmingPluginTest', 'Multi-Release': true)
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from sourceSets.main.output
	from sourceSets.test.output
	from {
		configurations.testRuntimeClasspath.collect { file ->
			file.isDirectory() ? file : zipTree(file)
		}
	}

	exclude 'META-INF/INDEX.LIST'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	exclude '**/module-info.class'

	group = BasePlugin.BUILD_GROUP
	archiveClassifier.set('shadow')
	archiveFileName.set("${rootProject.name}-${project.version}-all.jar")
}